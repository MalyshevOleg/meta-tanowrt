#@TYPE: Machine
#@NAME: AM574x IDK EVM
#@DESCRIPTION: Machine configuration for the AM574x IDK EVM sdcard

require conf/machine/include/am574x-evm.inc

#
# Machine/distro features
#
MACHINEOVERRIDES =. "am574x-idk:"

MACHINE_FEATURES = "\
	apm usbgadget usbhost vfat ext2 ext4 screen \
	touchscreen graphics gpu mmip dsp gc320 pci \
	sgx ethernet cgroup lldp stp watchdog \
	mbusgw \
"

#
# Graphics
#
MACHINE_GUI_CLASS = "smallscreen"

#
# Kernel
#
KERNEL_IMAGETYPE = "zImage"
KERNEL_DEVICETREE = " \
	am57xx-evm.dtb \
	am57xx-beagle-x15.dtb \
	am57xx-beagle-x15-revb1.dtb \
	am57xx-beagle-x15-revc.dtb \
	am571x-idk.dtb \
	am572x-idk.dtb \
	am574x-idk.dtb \
"

SERIAL_CONSOLES = "115200;ttyS2"

#
# Bootloader
#
UBOOT_MACHINE = "am57xx_evm_defconfig"

#
# Image
#
IMAGE_CLASSES += "sdimage-am574x-helper"
IMAGE_FSTYPES += "squashfs wic"

MACHINE_EXTRA_RDEPENDS += "\
	${@bb.utils.contains('MACHINE_FEATURES', 'mmip', 'ipumm-fw', '', d)} \
	ffmpeg \
	cmem \
	cmem-mod \
"

# WIC
WKS_FILE = "sdimage-am574x.wks"
IMAGE_BOOT_FILES = "${KERNEL_IMAGETYPE} MLO u-boot.img ${@parse_dtbs(d)}"
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"

PACKAGECONFIG_append_pn-fstools = " ovl-f2fs ovl-rootdisk-part "

IMAGE_FEATURES_remove = "modem"

IMAGE_FEATURES += "splash"
SPLASH = "psplash"

#
# OpenWrt specific
#
OPENWRT_VERSION_PRODUCT = "AM574x IDK EVM"
OPENWRT_VERSION_HWREV = "1.0"

#
# Other
#

# Refine the list of device targets for ti-pdk class recipes
# Set the list of device targets for ti-pdk class recipes
TI_PDK_LIMIT_SOCS = "am571x am572x am574x"
TI_PDK_LIMIT_BOARDS = "evmAM571x evmAM572x idkAM571x idkAM572x idkAM574x"
